buildscript {
    ext.pythonPluginVersion = pythonPluginVersion
    ext.scalaPluginVersion = scalaPluginVersion
    ext.pycharmSandbox = project.buildDir.absolutePath + File.separator + "pycharm-sandbox"
    ext.studioSandbox = project.buildDir.absolutePath + File.separator + "studio-sandbox"
    ext.kotlinVersion = "1.2.41"
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.3.4" apply false
    id "java"
    id "idea"
    id "de.undercouch.download" version "3.4.3"
}


group = 'org.jetbrains.edu'
def buildNumber = System.getenv("BUILD_NUMBER")
version = "$pluginVersion-${buildNumber == null ? "SNAPSHOT" : buildNumber}"







import de.undercouch.gradle.tasks.download.Download
import org.apache.tools.ant.taskdefs.condition.Os

import java.nio.file.Files

String downloadStudioIfNeededAndGetPath() {
    if (!rootProject.hasProperty("studioVersion")) throw IllegalStateException("studioVersion is unspecified")
    if (!rootProject.hasProperty("studioBuildVersion")) throw IllegalStateException("studioBuildVersion is unspecified")
    def osFamily = getOsFamily()
    if (osFamily == null) throw IllegalStateException("current os family is unsupported")

    def studioZip = file("${rootProject.projectDir}/dependencies/studio-$studioVersion-$studioBuildVersion-${osFamily}.zip")
    if (!studioZip.exists()) {
        download {
            src "http://dl.google.com/dl/android/studio/ide-zips/$studioVersion/android-studio-ide-$studioBuildVersion-${osFamily}.zip"
            dest studioZip
        }
    }

    def studioFolder = file("${rootProject.projectDir}/dependencies/studio-$studioVersion-$studioBuildVersion")
    if (!studioFolder.exists()) {
        copy {
            from zipTree(studioZip)
            into studioFolder
        }
    }
    return "$studioFolder/${studioContentPath()}"
}

static String getOsFamily() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) return "windows"
    if (Os.isFamily(Os.FAMILY_MAC)) return "mac"
    if (Os.isFamily(Os.FAMILY_UNIX) && !Os.isFamily(Os.FAMILY_MAC)) return "linux"
    return null
}

static String studioContentPath() {
    def osFamily = getOsFamily()
    if (osFamily == null) throw IllegalStateException("current os family is unsupported")
    if (osFamily == "mac") {
        return "Android Studio.app/Contents"
    } else {
        return "android-studio"
    }
}

static void createProperties(File location, LinkedHashMap<String, String> properties) {
    createFile(location.toPath())
    Properties props = new Properties()

    properties.each {key, value -> props.setProperty(key, value)}
    props.store(location.newWriter(), null)
}

static void createFile(java.nio.file.Path path) {
    def parentDirectory = path.parent
    Files.createDirectories(parentDirectory)
    if (!Files.exists(path)) {
        Files.createFile(path)
    }
}

task downloadJavaFx(type: Download) {
    overwrite true
    src javafx
    dest "${project.buildDir}/javafx.zip"
}

task prepareJavaFx(type: Copy) {
    def javafxFile = file("${project.buildDir}/javafx.zip")
    onlyIf { javafxFile.exists() }
    from zipTree(javafxFile)
    into file("${project.buildDir}/javafx")
}

prepareJavaFx.dependsOn downloadJavaFx

allprojects {
    apply plugin: "org.jetbrains.intellij"
    apply plugin: "java"
    apply plugin: "kotlin"


    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
    targetCompatibility = '1.8'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/markdown' }
    }

    intellij {
        if (project.hasProperty("ideaVersion")) {
            version ideaVersion
        } else if (project.hasProperty("ideaPath")) {
            localPath ideaPath
        }
    }

    compileKotlin {
        // see https://youtrack.jetbrains.com/issue/KT-19737
        destinationDir = compileJava.destinationDir
        kotlinOptions {
            jvmTarget = "1.8"
            languageVersion = "1.1"
            apiVersion = "1.1"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            languageVersion = "1.1"
            apiVersion = "1.1"
        }
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        compile group: 'org.twitter4j', name: 'twitter4j-core', version: '4.0.1'
        compile 'org.jsoup:jsoup:1.11.2'
        compile("org.jetbrains:markdown:${markdownParserVersion}") {
            exclude module: 'kotlin-runtime'
            exclude module: 'kotlin-stdlib'
        }

        compile 'com.squareup.retrofit2:retrofit:2.4.0'
        implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
        compileOnly fileTree(dir: "${rootProject.buildDir}/javafx/jre/lib/ext")
    }
}


intellij {
    if (project.hasProperty("ideaVersion")) {
        version ideaVersion
    } else if (project.hasProperty("ideaPath")) {
        localPath ideaPath
    }
    if (project.hasProperty("customSinceBuild")) {
        patchPluginXml.sinceBuild = customSinceBuild
        patchPluginXml.untilBuild = customUntilBuild
    }
    patchPluginXml.changeNotes file("changes.html").getText()
    patchPluginXml.pluginDescription file("description.html").getText()
    pluginName 'EduTools'
    updateSinceUntilBuild true
    downloadSources false
    plugins 'junit', 'Kotlin', "PythonCore:$pythonPluginVersion", "org.intellij.scala:$scalaPluginVersion"
}

task configurePyCharm {
    doLast {
        if (!project.hasProperty("pycharmPath")) {
            throw new InvalidUserDataException("Path to PyCharm installed locally is needed\nDefine \"pycharmPath\" property")
        }
        intellij.sandboxDirectory pycharmSandbox
        intellij.alternativeIdePath pycharmPath
    }
}

task configureAndroidStudio {
    doLast {
        if (!project.hasProperty("androidStudioPath")) {
            throw new InvalidUserDataException("Path to Android Studio installed locally is needed\nDefine \"androidStudioPath\" property")
        }
        intellij.sandboxDirectory studioSandbox
        intellij.alternativeIdePath androidStudioPath
    }
}

task copyXmls(type: Copy) {
    def resultingMetaInf = "${sourceSets.main.output.resourcesDir}/META-INF"

    for (def subProject : project.subprojects) {
        from "${subProject.name}/resources/META-INF"
        into resultingMetaInf
        include "*.xml"
    }
}

jar.dependsOn(copyXmls)

task removeIncompatiblePyCharmPlugins(type: Delete) {
    doLast {
        file(pycharmSandbox + File.separator + "plugins" + File.separator + "python-ce").deleteDir()
        file(pycharmSandbox + File.separator + "plugins" + File.separator + "Scala").deleteDir()
    }
}

// we need this so as not to install python and Scala plugins on PyCharm
prepareSandbox.finalizedBy(removeIncompatiblePyCharmPlugins)

subprojects {
    sourceSets {
        main {
            java.srcDirs 'src'
            resources.srcDirs 'resources'
            kotlin.srcDirs 'src'
        }

        test {
            java.srcDirs 'testSrc'
            resources.srcDirs 'testResources'
            kotlin.srcDirs 'testSrc'
        }
    }

    project.tasks.getByPath("runIde").enabled false
    project.tasks.getByPath("prepareSandbox").enabled false
}

sourceSets {
    main {
        resources.srcDirs 'resources'
    }
}

configure(project(':educational-core')) {

    task downloadColorFile(type: Download) {
        overwrite false
        src 'https://raw.githubusercontent.com/ozh/github-colors/master/colors.json'
        dest "${projectDir}/resources/languageColors/colors.json"
    }

    task createCheckiOOAuthProperties() {
        createProperties(
          file("${projectDir}/resources/checkio/checkio-oauth.properties"),
          [
            "checkioClientId" : checkioClientId,
            "checkioClientSecret" : checkioClientSecret
          ]
        )
    }

    processResources.dependsOn(createCheckiOOAuthProperties)
}

configure([project(':Edu-Python'), project(':Edu-Java'), project(':Edu-Kotlin'), project(':Edu-Scala')]) {
    dependencies {
        compile project(':educational-core')
        testCompile project(':educational-core').sourceSets.test.output
    }
}

configure([project(':educational-core'), project(':Edu-Java')]) {
    intellij.plugins 'junit', 'properties', 'gradle', 'Groovy'
}

configure(project(':Edu-Kotlin')) {
    intellij.plugins 'junit', 'Kotlin', 'properties','gradle', 'Groovy'


    task createTwitterProperties() {
        createProperties(
          file("${projectDir}/resources/twitter/kotlin_koans/oauth_twitter.properties"),
          [
            "consumerKey" : consumerKey,
            "consumerSecret" : consumerSecret
          ]
        )
    }
    processResources.dependsOn(createTwitterProperties)
}

configure(project(':Edu-Python')) {
    intellij.plugins "PythonCore:$pythonPluginVersion"
}

configure(project(':Edu-Scala')) {
    intellij.plugins "org.intellij.scala:$scalaPluginVersion", 'junit', 'properties', 'gradle', 'Groovy'
}

runIde.systemProperty("-Didea.is.internal", "true")
runIde.systemProperty("-ea", "")

dependencies {
    compile project(':educational-core'), project(':Edu-Python'), project(':Edu-Kotlin'), project(':Edu-Java'), project(':Edu-Scala')
}

idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
        vcs = 'Git'
    }
}

allprojects {
    test {
        if (rootProject.hasProperty("stepik_test_client_secret")) {
            environment 'STEPIK_TEST_CLIENT_SECRET', stepik_test_client_secret
        }
    }
}